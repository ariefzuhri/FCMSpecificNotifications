plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.ariefzuhri.myspecificnotification"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "FCM_BASE_URL", '"https://fcm.googleapis.com"')
        buildConfigField("String", "FIREBASE_SERVER_KEY", localProperties['firebaseServerKey'])

        resValue("string", "fcm_default_notification_channel_id", "fcm_default_channel")
        resValue("string", "fcm_default_notification_channel_name", "New notification")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Android support
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    // Testing
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitExtVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    // Parsing the JSON format
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Network logging
    debugImplementation "com.github.chuckerteam.chucker:library:$chuckerVersion"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chuckerVersion"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-database"
    implementation "com.google.firebase:firebase-messaging"

    // Play services
    implementation "com.google.android.gms:play-services-auth:$playServicesAuthVersion"
}